#!/usr/bin/awk -f

BEGIN {
	ECHO = "1;37"
	HIGH = "31"

	# Check if the directory represents a channel
	ischan = system("test -S \"${INSOMNIA_DIR}/usr\"")

	# Generate ASCII lookup table
	for (i = 1; i < 127; i++)
		ascii = sprintf("%s%c", ascii, i)

	# See https://en.wikipedia.org/wiki/ANSI_escape_code#3/4_bit
	for (i = 31; i <= 36; i++)
		colors[colors_len++] = i
	for (i = 91; i <= 96; i++)
		colors[colors_len++] = i
}

function colortext(color, text) {
	return sprintf("\033[%sm%s\033[0m", color, text)
}

function nickcolor(nick) {
	split(nick, chars, "")

	c = 0
	for (i = 1; i <= length(nick); i++)
		c += index(ascii, chars[i])

	return colors[c % colors_len]
}

/^[0-9]+ \(.+\) .+$/ {
	nick = substr($2, 2, length($2) - 2)
	suffix = substr($0, length($0), 1)

	if (suffix == "\006")
		color = ECHO
	else
		color = nickcolor(nick)

	preamble = $1 " " sprintf("(%s)", colortext(color, nick))
	for (i = 3; i <= NF; i++)
		text = text " " $i

	# Highlight everything except suffix on match
	text = substr(text, 0, length(text) - 1)
	if (ischan == 0 && suffix == "\007")
		text = colortext(HIGH, text)

	$0 = preamble text suffix
	text = ""
}

{
	print
	fflush()
}
