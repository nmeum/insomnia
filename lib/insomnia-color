#!/usr/bin/awk -f

BEGIN {
	# Generate ASCII lookup table
	for (i = 1; i < 127; i++)
		ascii = sprintf("%s%c", ascii, i)

	# See https://en.wikipedia.org/wiki/ANSI_escape_code#3/4_bit
	for (i = 31; i <= 36; i++)
		colors[colors_len++] = i
	for (i = 91; i <= 96; i++)
		colors[colors_len++] = i
}

function nickcolor(nick) {
	split(nick, chars, "")

	c = 0
	for (i = 1; i <= length(nick); i++)
		c += index(ascii, chars[i])

	color = colors[c % colors_len]
	return sprintf("\033[%dm%s\033[0m", color, nick)
}

/^[0-9]+ \(.+\) .+$/ {
	# Strip enclosing parentheses from nick
	nick = substr($2, 2, length($2) - 2)

	text = $1 " " sprintf("(%s)", nickcolor(nick))
	for (i = 3; i <= NF; i++)
		text = text " " $i
	$0 = text
}

{
	print
	fflush()
}
