#!/usr/bin/awk -f

BEGIN {
	for (i = 1; i < 128; i++)
		ascii = sprintf("%s%c", ascii, i)

	COLORS_LEN = 0

	# See https://en.wikipedia.org/wiki/ANSI_escape_code#3/4_bit
	for (i = 31; i <= 36; i++)
		COLORS[COLORS_LEN++] = i
	for (i = 91; i <= 96; i++)
		COLORS[COLORS_LEN++] = i
}

function colorcode(word) {
	c = 0

	split(word, chars, "")
	for (i = 1; i <= length(word); i++)
		c += index(ascii, chars[i])

	return COLORS[c % COLORS_LEN]
}

/^[0-9]+ \(.+\) .+$/ {
	# Strip enclosing parentheses from nick
	nick = substr($2, 2, length($2) - 2)

	text = $1 " " sprintf("(\033[%dm%s\033[0m)", colorcode(nick), nick)
	for (i = 3; i <= NF; i++)
		text = text " " $i
	$0 = text
}

{
	print
	fflush()
}
