#!/usr/bin/awk -f

BEGIN {
	ECHO = "1;37"

	# Generate ASCII lookup table
	for (i = 1; i < 127; i++)
		ascii = sprintf("%s%c", ascii, i)

	# See https://en.wikipedia.org/wiki/ANSI_escape_code#3/4_bit
	for (i = 31; i <= 36; i++)
		colors[colors_len++] = i
	for (i = 91; i <= 96; i++)
		colors[colors_len++] = i
}

function colortext(color, text) {
	return sprintf("\033[%sm%s\033[0m", color, text)
}

function nickcolor(nick) {
	split(nick, chars, "")

	c = 0
	for (i = 1; i <= length(nick); i++)
		c += index(ascii, chars[i])

	return colors[c % colors_len]
}

/^[0-9]+ \(.+\) .+$/ {
	# Strip enclosing parentheses from nick
	nick = substr($2, 2, length($2) - 2)

	if (substr($0, length($0), 1) == "\006")
		color = ECHO
	else
		color = nickcolor(nick)

	text = $1 " " sprintf("(%s)", colortext(color, nick))
	for (i = 3; i <= NF; i++)
		text = text " " $i
	$0 = text
}

{
	print
	fflush()
}
