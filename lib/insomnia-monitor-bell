#!/usr/bin/awk -f

BEGIN {
	idfp = ENVIRON["INSOMNIA_DIR"] "/id"
	getline name < idfp
	close(name)

	# Initialize PRNG with epoch and store it
	srand()
	startup = srand()
}

function inbrackets(string, opening, closing) {
	lbracket = index(string, opening)
	rbracket = index(string, closing)

	return substr(string, lbracket + 1, rbracket - lbracket - 1)
}

function ischan(channel) {
	# This is a stripped down variant of IsValidChannel from girc
	# which only checks the first character to determine channels.

	first = substr(channel, 1, 1)
	return index("!#&*~+", first)
}

/^[0-9]* \[.+\] \(.+\) .+$/ && name != "" {
	source = inbrackets($0, "[", "]")
	if (!ischan(source))
		source = inbrackets($0, "(", ")")

	if (source != name && $1 > startup) {
		printf("%s\a\n", $0)
		fflush()
		next
	}
}

{
	print
	fflush()
}
